plugins {
    id "net.fabricmc.fabric-loom-remap" version "${loom_version}"
    id "com.diffplug.spotless" version "8.+"
    id "com.modrinth.minotaur" version "2.+"
    id "maven-publish"
}

version = "$project.mod_version+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "TerraformersMC Maven"
        url = "https://maven.terraformersmc.com/releases"
        content {
            includeGroup "com.terraformersmc"
        }
    }
    maven {
        name = "FzzyMaven"
        url = "https://maven.fzzyhmstrs.me/"
        content {
            includeGroup "me.fzzyhmstrs"
        }
    }
    maven {
        name = "Jared's Maven"
        url = "https://maven.blamejared.com/"
        content {
            includeGroup "mezz.jei"
        }
    }
    maven {
        url = "https://maven.shedaniel.me/"
        content {
            includeGroupAndSubgroups "me.shedaniel"
            includeGroup "dev.architectury"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:trutils:${project.trutils_version}"
    modImplementation "maven.modrinth:codec2schema:${project.codec2schema_version}"
    modImplementation "me.fzzyhmstrs:fzzy_config:${project.fzzy_config_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    if (project.recipe_viewer == "jei") {
        modRuntimeOnly "mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}"
    }
    modCompileOnly "mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}"

    if (project.recipe_viewer == "rei") {
        modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    }
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${project.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:${project.rei_version}"
}

apply from: "https://raw.githubusercontent.com/Tr100000/modding-utils/main/scripts/devlogin.gradle"

loom.splitEnvironmentSourceSets()

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.output
        runtimeClasspath += main.output

        compileClasspath += client.compileClasspath
        runtimeClasspath += client.runtimeClasspath
        compileClasspath += client.output
        runtimeClasspath += client.output
    }
}

loom {
    mods {
        "researcher" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    accessWidenerPath = file("src/main/resources/researcher.classtweaker")

    runs {
        testmod {
            client()
            name = "Testmod Client"
            source = sourceSets.testmod
            ideConfigGenerated = true
            programArgs "--launch_target", "net.fabricmc.loader.impl.launch.knot.KnotClient"
            mainClass.set "net.covers1624.devlogin.DevLogin"
        }
    }
}

modrinth {
    projectId = "researcher"
    versionNumber = project.version
    versionName = project.version
    uploadFile = remapJar
    additionalFiles = [remapSourcesJar]
    changelog = rootProject.file("CHANGELOG.md").text
    syncBodyFrom = rootProject.file("README.md").text
    dependencies {
        required.project "fabric-api"
        required.project "trutils"
        required.project "fzzy-config"
    }
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

spotless {
    format "misc", {
        target "*.gradle", "*.properties", ".gitattributes", ".gitignore"

        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    withSourcesJar()
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
}
